apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
	compileSdkVersion 30

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 30
		versionCode 1
		versionName rootProject.ext.libraryVersion
	}
	buildTypes {
		release {
			minifyEnabled false
		}
	}
	buildFeatures {
		buildConfig = false
		resValues = false
		shaders = false
		viewBinding = false
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = "1.8"
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

group "eu.alessiobianchi"
def artifact_id = "log"
version android.defaultConfig.versionName

task androidSourcesJar(type: Jar) {
	archiveClassifier.set('sources')
	from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.release
				artifact androidSourcesJar

				artifactId = artifact_id
			}
		}
	}
}

// gradle -Papikey=ASDFASDFASDF bintrayUpload
bintray {
	def bintrayUser = "venator85"
	def bintrayApiKey = project.findProperty('apikey') ?: System.getenv('BINTRAY_API_KEY')
	user = bintrayUser
	key = bintrayApiKey
	publications = ['release']
	pkg {
		repo = 'maven'
		name = artifact_id
		userOrg = bintrayUser
		publish = true
	}
	override = true
	dryRun = false
}
